trackCardHtml = (track, voteable = true) ->
  return blankTrackCardHtml() if track.blank == true || track.blank == "true"
  return """
    <div class="track-card #{"voteable" if voteable}" data-track-id="#{track.id}">
      <div class="track-card-overlay">
        <div class="btn vote-btn btn-success btn-xl">vote</div>
      </div>
      <img src='#{track.image_med.url}'>
      <div class="track-info">
        <div class="track-name"><em>Track: </em>#{track.name}</div>
        <div class="track-artist"><em>Artist: </em>#{track.artist}</div>
        <div class="track-album-name"><em>Album: </em>#{track.album_name}</div>
        <div class="track-duration"><em>#{track.duration_readable}</em></div>
      </div>
    </div>
  """

blankTrackCardHtml = () ->
  return """
    <div class="track-card blank-track-card">
      <h1> ad. </h1>
    </div>
  """

voteContainerHtml = () ->
  return """
  <div class="vote-container">
    <span>Votes</span>
    <span class="vote-count">0</span>
  </div>
  """

pusherChannel = ()->
  return $('#channel-container').attr('data-channel-name')


isAlreadyNominated = (track)->
  return nominatedTrackIds().includes(track.id)

nominatedTrackIds = ()->
  return $('#vote-results .track-card').map(()-> 
    return $(this).attr('data-track-id')
  ).get()

addVote = (track)->
  $card = $(getCardByTrackId(track.id))
  currentVoteCount = parseInt($card.find('.vote-count').text())
  $card.find('.vote-count').text(currentVoteCount + 1)

getCardByTrackId = (id)->
  return $("#vote-results .track-card[data-track-id='#{id}']")

addVoteContainerTo = ($card)->
  console.log($card)
  $(voteContainerHtml()).insertBefore($card.find('.track-info'));

initTrackCardsData = ()->
  # Hmm a bit fragile. These fields need to match what is set in
  # SpotifyUtilities
  $('.track-card').each (i)->
    $card = $(this)
    track = {
      album_name: $card.attr('data-album-name'),
      artist: $card.attr('data-artist'),
      duration_ms: $card.attr('data-duration-ms'),
      duration_readable: $card.attr('data-duration-readable'),
      id: $card.attr('data-id'),
      name: $card.attr('data-name'),
      image_sm: $card.attr('data-image-sm'),
      image_med: $card.attr('data-image-med'),
      image_lg: $card.attr('data-image-lg'),
      vote_count: $card.attr('data-vote-count')
      uri: $card.attr('data-uri')
    }
    $card.data({track})

clickVoteEvent = ()->
  console.log "clicked"
  trackData = $(this).data('track')
  $.post(
    window.location.pathname + '/vote',
    {
      vote: trackData,
      authenticity_token: $('[name="csrf-token"]')[0].content
    },
    (result)->
      return
  )

initEventHandlers = ()->
  if $('#channel-container').attr('data-vote-status') == "true"
    $(document).on 'click', '.voteable', clickVoteEvent

####

class App.Channels
  constructor: () ->
    # intialize some stuff

  render: ->
    # do some stuff

$ ->
  return unless $(".channels.show").length > 0 || $(".channels.host").length > 0

  initTrackCardsData()
  initEventHandlers()

  $('#search_track_form').on 'ajax:success', (data) ->
    tracks = data.detail[0]
    $('#search-results').empty()
    $.each tracks, (i, track) ->
      $card = $(trackCardHtml(track)).appendTo('#search-results')
      $card.data('track', track)
    return

  pusher = new Pusher(
    '<%= ENV["pusher_key"] %>',
    cluster: '<%= ENV["pusher_cluster"] %>',
    encrypted: true,
    authEndpoint: '/pusher/auth',
    auth: {
      headers: {
        'X-CSRF-Token': FORM_AUTH_TOKEN
      }
    }
  )
 
  channel = pusher.subscribe(pusherChannel())
  presenceChannel = pusher.subscribe("presence-#{pusherChannel()}")

  channel.bind 'new_vote', (track) ->
    console.log('new vote!')
    # Handle this server-side?
    if isAlreadyNominated(track)
      console.log 'already nominated!'
      addVote(track)
    else
      $card = $(trackCardHtml(track)).appendTo('#vote-results')
      $card.data('track', track)
      addVoteContainerTo($card)
      addVote(track)
    return
  
  channel.bind 'current_track', (track) ->
    console.log('current track updated!')
    console.log( track )
    if !!track
      $('#up-next-results').empty()
      $('#current-track-results').empty()
      $card = $(trackCardHtml(track, false)).appendTo('#current-track-results')
    return

  # We do not want this binding on the host's page
  if $(".channels.host").length == 0
    channel.bind 'host_presence', (presence) ->
      console.log "presence changed!"
      if presence == true
        # do something if host joins
      else
        # do something if host leaves
      
      # reload page regardless
      location.reload();
      return
    

  channel.bind 'vote_status', (status) ->
    if status == "open"
      # voting has just been opened. Remove 'current votes'
      # TODO: Hmmm we don't necessarily want to do this?
      console.log "removing votes!"
      $('#vote-container .track-card').remove()
      $(document).on 'click', '.voteable', clickVoteEvent
      # hide voting closed message
      $('#channel-notification-box').addClass('d-none')
    else if status == "closed"
      $(document).off 'click', '.voteable', clickVoteEvent
      #display voting closed message
      $('#channel-notification-box').removeClass('d-none')
    return

  channel.bind 'next_track', (next_track) ->
    console.log('next track selected!')
    if !!next_track
      $('#up-next-results').empty()
      $card = $(trackCardHtml(next_track, false)).appendTo('#up-next-results')
    return


  # hmm we have our own event to handle host leaving.
  presenceChannel.bind 'pusher:member_removed', (member) ->
    console.log member
    if member.info.is_host
      console.log 'host left!'
    else
      console.log 'voter left!'
      # decrease viewer count?
    return

  presenceChannel.bind 'pusher:member_added', (member) ->
    console.log member
    if member.info.is_host
      console.log 'host joined!'
    else
      console.log 'voter joined!'
      # increase viewer count?
    return

  

  return
